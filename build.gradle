plugins {
    id 'java-library'
    id "maven-publish"
}

group = 'app.ister'
version = '1.1.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:6.0.1"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.1'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:6.0.1"
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.named('test') {
    useJUnitPlatform()
    jvmArgs '-agentlib:native-image-agent=config-merge-dir=src/main/resources/META-INF/native-image'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'blurhash'

            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'BlurHash for Java'
                description = 'A BlurHash encoder implementation in Java'
                url = 'https://github.com/ister-app/blurhash-java'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'hendrik'
                        name = 'Hendrik'
                        email = 'hendrik@h5k.org'
                    }
                    developer {
                        id = 'tijder'
                        name = 'Gerben'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/ister-app/blurhash-java"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
